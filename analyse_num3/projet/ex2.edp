// Construction du maillage de l'appartement 
real Lx = 7.0;      // Longueur totale
real Ly = 5.0;      //Largeur totale
real murep = 0.2;  //Epaisseur du mur
real portew = 0.8; // Largeur de la porte
real xgauche = 4.7; // Coordonnee x du mur gauche
real xdroite = xgauche+0.2; // Coordonnee x du mur droit
real yportebas = 2.7; // coordonnée bas de la porte
real yportehaut = yportebas + portew; // Coordonnée y du haut de la porte


border basgauche(t=0,xgauche){ x=t; y=0; label=1; };  
border murbasgauche(t=0,yportebas){ x=xgauche; y=t; label=2; };  
border murbas(t=xgauche,xdroite){ x=t; y=yportebas; label=3; };  
border murbasdroite(t=0,yportebas){ x=xdroite; y=yportebas-t; label=4; };  
border basdroite(t=xdroite,Lx){ x=t; y=0; label=5; };  
border murdroit(t=0,Ly){ x=Lx; y=t; label=6; };  

border hautdroit(t=Lx,xdroite){ x=t; y=Ly; label=7; }; 
border murhautdroit(t=Ly,yportehaut){ x=xdroite; y=t; label=8; }; 
border murhaut(t=0,murep){ x=xdroite-t; y=yportehaut; label=9; };  
border murhautgauche(t=yportehaut,Ly){ x=xgauche; y=t; label=10; };  
border hautgauche(t=0,xgauche){ x=xgauche-t; y=Ly; label=11; };  
border murgauche(t=0,Ly){ x=0; y=Ly-t; label=12; };  



mesh Th = buildmesh(basgauche(20) + murbasgauche(20) + murbas(10) + murbasdroite(20) + basdroite(20) + murdroit(20) + murgauche(20) + hautgauche(20) + hautdroit(20) + murhautgauche(20) + murhautdroit(20) + murhaut(10)); 
//plot(Th, wait=1,fill=1);



// === Espaces fonctionnels
fespace Vh(Th, P1);
Vh u1, u2, v1, v2;

// === Paramètres des sources
real x1 = 1.5, y1 = 1.5;
real x2 = 6.0, y2 = 3.5;

func f1 = 100*exp(-((x-x1)^2+(y-y1)^2));
func f2 = -50*exp(-((x-x2)^2+(y-y2)^2));

// === Problème variationnel
problem thermic([u1,u2],[v1,v2])
    = int2d(Th)(
        dx(u1)*dx(v1) + dy(u1)*dy(v1) - u2*v1
      + dx(u2)*dx(v2) + dy(u2)*dy(v2) + u2*v2 + u1*v2
    )
    - int2d(Th)(f1*v1 + f2*v2)
    + on(1,2,3,4,5,6,7,8,9,10,11,12, u1=0); // Dirichlet sur u1 uniquement

thermic;

// === Visualisation
plot(u1, wait=1,value=true,fill=true,nbiso=20); // Visualisation de la température u1
plot(u2, wait=1,value=true,fill=true,nbiso=20); // Visualisation de la vitesse u2
plot([u1,u2], wait=1); // Visualisation du champ de vitesse